//
// Подсчеты для перемещения книг на открытый доступ
// в рамках трансформации в модельную библиотеку
//

connect("host=127.0.0.1;port=6666;user=librarian;password=secret;db=IBIS;")
if (!is_connected())
{
    error ("Not connected")
    exit()
}

//=================================

books = batch_read (true)
foreach (book in books)
{
    if (is_book (book) and is_text (book))
    {
        total = 0
        fkh = []  // основное книгохранилище
        f201 = [] // ЧЗ естественнонаучный
        f202 = [] // ЧЗ гуманитарный
        f506 = [] // ОЛИ книги
        foreach (one in get_exemplars (book))
        {
            status = one.Status
            number = one.Number
            if (number
                and have_subfield (one.Field, 'h')
                and (status == "0" or status == "1" or status == "5" or status == "9"))
            {
                total = total + 1

                if (one.Place == "ФКХ")
                    fkh.Add (number)

                if (one.Place == "Ф201")
                    f201.Add (number)

                if (one.Place == "Ф202")
                    f202.Add (number)

                if (one.Place == "Ф506")
                    f506.Add (number)
            }
        }

        if (total)
        {
            brief = format_record ("@brief", book.Mfn)
            rental = book_rental_count (book)
            year = book_year (book)
            bbk = book_classification (book)

            println (book.Mfn, "\t", total, "\t", rental, "\t",
                year, "\t", bbk, "\t", join (", ", fkh), "\t",
                join (", ", f201), "\t", join (", ", f202), "\t",
                join (", ", f506), "\t", brief)
        }
    }
}

//=================================

disconnect()
println ("THAT'S ALL, FOLKS!")
